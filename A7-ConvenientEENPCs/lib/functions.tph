// Various functions for disabling individual NPCs

DEFINE_ACTION_FUNCTION A7DisableBaeloth
BEGIN
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    OUTER_TEXT_SPRINT script ~%bg1_area_prefix%2900.BCS~
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%~) BEGIN
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BAINVI","GLOBAL",0)~ ~False()~
        END BUT_ONLY
    END
  END
END

DEFINE_ACTION_FUNCTION A7DisableDorn
BEGIN
  ACTION_IF (GAME_IS ~eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~BG2301.BCS~ ~BG4800.BCS~ ~OH1000.BCS~ ~AR0900.BCS~ ~FATESP.DLG~ END
  END ELSE ACTION_IF (GAME_IS ~bgee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR2301.BCS~ ~AR4800.BCS~ ~OH1000.BCS~ END
  END ELSE ACTION_IF (GAME_IS ~bg2ee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR0900.BCS~ ~FATESP.DLG~ END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN END
  END
  LAF A7Disable#DLC STR_VAR file_array = a7files END

  ACTION_IF (FILE_EXISTS_IN_GAME ~bd2000.bcs~) BEGIN
    COPY_EXISTING ~bd2000.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("bd_dorn_spawn","bd2000",0)~ ~False()~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BD_dorn_map_note","BD2000",0)~ ~False()~
      END BUT_ONLY
  END

  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    OUTER_SET bg1_strref_18576 = bg1_strref + 18576
    COMPILE EVAL ~%MOD_FOLDER%/dialogs/dorn_bg1.d~
  END
END

DEFINE_ACTION_FUNCTION A7DisableHexxat
BEGIN
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR0406.BCS~ ~AR5501.BCS~ ~OH7000.BCS~ ~FATESP.DLG~ END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN END
  END

  LAF A7Disable#DLC STR_VAR file_array = a7files END
END

DEFINE_ACTION_FUNCTION A7DisableNeera
BEGIN
  ACTION_IF (GAME_IS ~eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~BG3300.BCS~ ~OHNMCUT1.BCS~ ~FATESP.DLG~ END
  END ELSE ACTION_IF (GAME_IS ~bgee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR3300.BCS~ END
  END ELSE ACTION_IF (GAME_IS ~bg2ee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~OHNMCUT1.BCS~ ~FATESP.DLG~ END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN END
  END
  LAF A7Disable#DLC STR_VAR file_array = a7files END

  ACTION_IF (FILE_EXISTS_IN_GAME ~bd2100.bcs~) BEGIN
    COPY_EXISTING ~bd2100.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BD_NEERA","BD2100",0)~ ~False()~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BD_neera_map_note","BD2100",0)~ ~False()~
      END BUT_ONLY
  END
END

DEFINE_ACTION_FUNCTION A7DisableRasaad
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    OUTER_TEXT_SPRINT rasaad_script ~%bg1_area_prefix%4800.BCS~
    ACTION_IF (FILE_EXISTS_IN_GAME ~%rasaad_script%~) BEGIN
      COPY_EXISTING ~%rasaad_script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("RASAAD_SPAWN_STOP","GLOBAL",0)~ ~False()~
        END BUT_ONLY
    END

    ACTION_IF (FILE_EXISTS_IN_GAME ~bd7000.bcs~) BEGIN
      COPY_EXISTING ~bd7000.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("bd_rasaad_spawn","bd7000",0)~ ~False()~
        END BUT_ONLY
    END
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR2000.BCS~ ~FATESP.DLG~ END
    LAF A7Disable#DLC STR_VAR file_array = a7files END

    ACTION_IF (FILE_EXISTS_IN_GAME ~ohr_join.bcs~) BEGIN
      COPY_EXISTING ~ohr_join.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("OHR_PLOT","GLOBAL",0)~ ~False()~
        END
    END
  END
END


DEFINE_ACTION_FUNCTION A7PassiveBaeloth
BEGIN
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    COMPILE ~%MOD_FOLDER%/scripts/cutba01.baf~
  END
END

DEFINE_ACTION_FUNCTION A7PassiveHexxat
BEGIN
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~OHHFAK.BCS~) BEGIN
      COPY_EXISTING ~OHHFAK.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~Num.*TimesTalkedTo(0)~ ~False()~
        END BUT_ONLY
    END
  END
END

DEFINE_ACTION_FUNCTION A7PassiveNeera
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    ACTION_IF (GAME_IS ~eet~) BEGIN
      OUTER_TEXT_SPRINT script ~NEERA_.BCS~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT script ~NEERA.BCS~
    END
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%~) BEGIN
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("NEERA_MEETING",.+)~ ~False()~
        END BUT_ONLY
    END
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~OHNCUT1A.BCS~) BEGIN
      COPY_EXISTING ~OHNCUT1A.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("NEERA.+)~ ~~
        END BUT_ONLY
      EXTEND_BOTTOM ~ohncut1a.bcs~ ~%MOD_FOLDER%/scripts/ohncut1a_neera.baf~
      COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts/ohnmcut1.baf~
      COMPILE ~%MOD_FOLDER%/scripts/a7_ncut1.baf~
      COMPILE ~%MOD_FOLDER%/dialogs/neera_bg2.d~
      COPY_EXISTING ~OH6010.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          // fixing initial viewpoint location in Neera's random encounter area
          REPLACE_TEXTUALLY ~\(Explore()\)~ ~MoveViewPoint([533.869],INSTANT) \1~
        END
      BUT_ONLY
    END
  END
END

DEFINE_ACTION_FUNCTION A7PassiveRasaad
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    ACTION_IF (GAME_IS ~eet~) BEGIN
      OUTER_TEXT_SPRINT script ~RASAAD_.BCS~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT script ~RASAAD.BCS~
    END
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%~) BEGIN
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          // Rasaad should not interfere with the Nimbul or Neira fights
          SET ofs = 0
          WHILE (ofs >= 0) BEGIN
            SET ofs = INDEX_BUFFER(~"NIMBUL"~ (ofs + 1))
            PATCH_IF (ofs >= 0) BEGIN
              SET ofs1 = RINDEX_BUFFER(CASE_SENSITIVE ~^IF~ ofs)
              SET ofs2 = INDEX_BUFFER(CASE_SENSITIVE ~^END~ ofs)
              // Disable attack
              SET ofs3 = INDEX_BUFFER(~^[ %TAB%]*Attack.*(~ ofs1)
              PATCH_IF (ofs3 > ofs1 && ofs3 < ofs2) BEGIN
                TEXT_SPRINT comment ~//~
                SET comment_len = STRING_LENGTH ~%comment%~
                INSERT_BYTES ofs3 comment_len
                WRITE_ASCIIE ofs3 ~%comment%~ (comment_len)
                SET ofs2 += comment_len
              END
              // Prevent auto-talk
              SET ofs3 = INDEX_BUFFER(~^[ %TAB%]*Dialog.*(~ ofs1)
              PATCH_IF (ofs3 > ofs1 && ofs3 < ofs2) BEGIN
                TEXT_SPRINT talked ~SetNumTimesTalkedTo(1) //~
                SET talked_len = STRING_LENGTH ~%talked%~
                INSERT_BYTES ofs3 talked_len
                WRITE_ASCIIE ofs3 ~%talked%~ (talked_len)
                SET ofs2 += talked_len
              END
            END
          END
        END BUT_ONLY
    END
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~RASAAD.BCS~) BEGIN
      COPY_EXISTING ~ar2000.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("RASAAD8",.*)~ ~CreateCreature("RASAAD8",[1832.1410],SSE)~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~MoveGlobal("AR2000","Rasaad",.*)~ ~MoveGlobal("AR2000","Rasaad",[1832.1410])~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("Rasaad",Face(.*))~ ~ActionOverride("Rasaad",Face(SSE))~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("OHRTREYA",.*)~ ~~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("OHRSSM01",.*)~ ~~
        END BUT_ONLY
      COPY_EXISTING ~ohrssm01.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~Range(\[PC\],.*)~ ~Detect([PC])~
        END BUT_ONLY
      COMPILE ~%MOD_FOLDER%/dialogs/rasaad_bg2.d~
    END
  END
END


// Caelar Argent always joins as follower if invited in SoD Avernus boss battle
DEFINE_ACTION_FUNCTION A7CaelarFollower
INT_VAR
  forced = 0  // 0: Caelar joins party if free slot available, 1: Caelar always joins as follower regardless of party size
BEGIN
  ACTION_IF (forced) BEGIN
    COMPILE ~%MOD_FOLDER%/dialogs/caelar_sod.d~
  END

  // Make Caelar controllable in the battle
  COPY_EXISTING ~bdcaelar.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      // Modify script block to make kicked out Caelar controllable
      REPLACE_TEXTUALLY ~ChangeEnemyAlly(Myself,GOODBUTBLUE)~
                        ~SetGlobal("a7_caelar_follower","locals",1) ChangeEnemyAlly(Myself,FAMILIAR) AddFamiliar()~

      // Add script blocks before first instance of plot check
      SET idx = INDEX_BUFFER(CASE_INSENSITIVE ~\bGlobal("bd_plot","global",570)~)
      PATCH_IF (idx >= 0) BEGIN
        SET idx = RINDEX_BUFFER(CASE_SENSITIVE ~^IF$~ idx)
        PATCH_IF (idx >= 0) BEGIN
          INSERT_FILE idx ~%MOD_FOLDER%/scripts/bdcaelar_follower.baf~
        END
      END
    END
  BUT_ONLY IF_EXISTS
END


// Internally used
DEFINE_ACTION_FUNCTION A7Disable#DLC
  STR_VAR
    file_array  = ~~
BEGIN
  ACTION_IF VARIABLE_IS_SET $EVAL "%file_array%"(0) BEGIN
    ACTION_PHP_EACH "%file_array%" AS _ => filename BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%filename%~) BEGIN
        COPY_EXISTING ~%filename%~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE ~HasDLC(.+)~ ~False()~
          END BUT_ONLY
      END
    END
  END
END


// Add Baeloth's items to the game
DEFINE_ACTION_FUNCTION A7ItemsBaeloth
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~ AND NOT FILE_EXISTS_IN_GAME ~bd1000.bcs~) BEGIN   // excluding SoD
    // Slave Ring
    COPY_EXISTING ~baring.itm~ ~override~
      WRITE_LONG NAME1 6348   // Ring
      WRITE_LONG 0x18 ((LONG_AT 0x18) | 0x04 | 0x10 | 0x40)   // enabling Droppable, Cursed and Magical flags
      WRITE_BYTE 0x2f ((BYTE_AT 0x2f) | 0x02)   // Unusable by: Wizard Slayer
      WRITE_LONG 0x34 50    // Price
      WRITE_SHORT 0x42 50   // Lore
      WRITE_LONG UNIDENTIFIED_DESC 17054  // Generic ring description...
      SAY IDENTIFIED_DESC @50000          // This ring has been known to be in possession of a drow torturer...
      LPF ALTER_ITEM_EFFECT
        INT_VAR
        check_headers = 1
        match_opcode = 122    // Create inventory item
        parameter1 = 1        // # to create
      END
      LPF DELETE_ITEM_EQEFFECT
        INT_VAR
        opcode_to_delete = 319  // Restrict item to Baeloth
      END
    BUT_ONLY

    COPY_EXISTING ~stocheap.sto~ ~override~
      ADD_STORE_ITEM ~baring~ FIRST #1 #0 #0 ~NONE~ #1
    BUT_ONLY

    COPY_EXISTING ~item_use.2da~ ~override~
      LPF REMOVE_TEXT_LINE STR_VAR search = ~^[ %TAB%]*BARING\b~ END
    BUT_ONLY
  END
END


// Add Dorn's items to the game
DEFINE_ACTION_FUNCTION A7ItemsDorn
BEGIN
  // Rancor +1
  COPY_EXISTING ~sw2hd1.itm~ ~override~
    // remove Dorn references from item description
    SAY IDENTIFIED_DESC @50006  // This ebony two-handed sword is covered with...

    // make weapon's combat ability available to everyone
    LPF DELETE_ITEM_EQEFFECT
      INT_VAR opcode_to_delete = 232
    END
    PATCH_FOR_EACH resource IN ~OHDSW2~ ~OHDSW1~ BEGIN
      LPF ADD_ITEM_EQEFFECT
        INT_VAR
          opcode = 232    // Cast spell on condition
          target = 1      // Self
          parameter2 = 12 // Killed([ANYONE]) / instant
          timing = 2      // While equipped
        STR_VAR
          resource
      END
    END
  BUT_ONLY

  // THAC0 bonus should last for 24 hours
  COPY_EXISTING ~ohdsw2.spl~ ~override~
    LPF ALTER_SPELL_EFFECT
      INT_VAR
        match_opcode = 206  // Protection from spell
        timing = 0          // Instant/Limited
        duration = 7200     // 24 h
    END
  BUT_ONLY

  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    // Gloves of Missile Snaring
    COPY_EXISTING ~ulgoth.sto~ ~override~
      ADD_STORE_ITEM ~brac18~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    COPY_EXISTING ~taerum.sto~ ~override~
      // Golden Girdle of Urnst
      ADD_STORE_ITEM ~belt02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
      // Albruin +1
      ADD_STORE_ITEM ~sw1h34~ LAST #1 #0 #0 ~IDENTIFIED~ #1
      // Elven Chain Mail
      ADD_STORE_ITEM ~chan12~ AFTER ~chan01~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Amulet of Spell Warding
    COPY_EXISTING ~inn3351.sto~ ~override~
      ADD_STORE_ITEM ~amul25~ AT 1 #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Rancor +1
    COPY_EXISTING ~gardus.cre~ ~override~
      ADD_CRE_ITEM ~sw2hd1~ #0 #0 #0 ~NONE~ ~WEAPON2 WEAPON3 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Fallorain's Plate
    COPY_EXISTING ~tam.cre~ ~override~
      ADD_CRE_ITEM ~plat02~ #0 #0 #0 ~NONE~ ~ARMOR INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Silver Dragon Scales
    COPY_EXISTING ~udsilver.cre~ ~override~
      ADD_CRE_ITEM ~ohdscale~ #0 #0 #0 ~UNSTEALABLE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
    COPY_EXISTING ~gorwom02.cre~ ~override~
      ADD_CRE_ITEM ~ohdscale~ #0 #0 #0 ~UNSTEALABLE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Holy Long Sword of Tyr +3
    COPY_EXISTING ~ar3014.are~ ~override~
      LPF ADD_AREA_ITEM
        INT_VAR
        container_to_add_to = 1
        STR_VAR
        item_to_add = ~OHDHSWD~
      END
    BUT_ONLY

    // Rancor +1
    COPY_EXISTING ~demfig01.cre~ ~override~
      ADD_CRE_ITEM ~sw2hd1~ #0 #0 #0 ~NONE~ ~WEAPON2 WEAPON3 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Wand of Glitterdust
    COPY_EXISTING ~ohdwand1.itm~ ~override~
      // update location-specific references in item description
      SAY IDENTIFIED_DESC @50007  // Found amongst a random formation of rocks in the Windspear Hills,...
    BUT_ONLY
    COPY_EXISTING ~ar1200.are~ ~override~
      LPF ADD_AREA_ITEM
        INT_VAR
        container_to_add_to = 5
        charges1 = 5
        STR_VAR
        item_to_add = ~OHDWAND1~
      END
    BUT_ONLY

    // Plate of the Dark +1
    COPY_EXISTING ~uddoor02.cre~ ~override~
      ADD_CRE_ITEM ~plat05~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Helm of the Noble +1
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~helm03~ AFTER ~helm13~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
    COPY_EXISTING ~trmer04.sto~ ~override~
      ADD_STORE_ITEM ~helm03~ FIRST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Rod of Shadowstep
    COPY_EXISTING ~amsaemon.cre~ ~override~
      ADD_CRE_ITEM ~ohdrod~ #5 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Potion of Clairvoyance
    COPY_EXISTING ~roger.sto~ ~override~
      ADD_STORE_ITEM ~ohdpotn1~ LAST #1 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
    COPY_EXISTING ~sartem01.sto~ ~override~
      ADD_STORE_ITEM ~ohdpotn1~ AFTER ~potn20~ #1 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Abyssal Blade
    COPY_EXISTING ~suraam.cre~ ~override~
      ADD_CRE_ITEM ~ohdsw2ha~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // The Brass Blade +5
    COPY_EXISTING ~hgber01.cre~ ~override~
      ADD_CRE_ITEM ~ohdsw03~ #1 #0 #0 ~NONE~ ~WEAPON1 WEAPON2 WEAPON3 WEAPON4 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // The Visage
    COPY_EXISTING ~uddeath2.cre~ ~override~
      ADD_CRE_ITEM ~ohdmask~ #1 #1 #0 ~NONE~ ~HELMET INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
  END
END


// Add Hexxat's items to the game
DEFINE_ACTION_FUNCTION A7ItemsHexxat
BEGIN
  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Ring of Anti-Venom
    COPY_EXISTING ~maevar.cre~ ~override~
      ADD_CRE_ITEM ~ring46~ #0 #0 #0 ~NONE~ ~LRING RRING INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Worn Whispers
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~boot02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // The Jade Fang
    COPY_EXISTING ~ribald3.sto~ ~override~
      ADD_STORE_ITEM ~ohhdag01~ AFTER ~dagg15~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Periapt of Life Protection
    COPY_EXISTING ~ar1401.are~ ~override~
      LPF ADD_AREA_ITEM
        INT_VAR
        container_to_add_to = 1
        STR_VAR
        item_to_add = ~AMUL23~
      END
    BUT_ONLY

    COPY_EXISTING ~ar18thif.cre~ ~override~
      // Shortbow +3
      ADD_CRE_ITEM ~bow26~ #0 #0 #0 ~NONE~ ~WEAPON2 WEAPON3 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
      // also add a score of Arrows +2
      ADD_CRE_ITEM ~arow11~ #20 #0 #0 ~NONE~ ~QUIVER1 QUIVER2 QUIVER3 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
  END
END


// Add Neera's items to the game
DEFINE_ACTION_FUNCTION A7ItemsNeera
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    // Gem Bag
    COPY_EXISTING ~friend.sto~ ~override~
      ADD_STORE_ITEM ~bag02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Adoy's Belt
    COPY_EXISTING ~inn3351.sto~ ~override~
      ADD_STORE_ITEM ~nebelt01~ AT 1 #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Rugged Leathers +2
    COPY_EXISTING ~caldo.cre~ ~override~
      ADD_CRE_ITEM ~leat25~ #0 #0 #0 ~NONE~ ~ARMOR INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Scroll of Stoneskin
    COPY_EXISTING ~denak.cre~ ~override~
      ADD_CRE_ITEM ~scrl2b~ #1 #1 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    COPY_EXISTING ~ribald.sto~ ~override~
      // Adoy's Belt
      ADD_STORE_ITEM ~nebelt01~ AFTER ~amul14~ #0 #0 #0 ~IDENTIFIED~ #1
      // Robe of Invocation
      ADD_STORE_ITEM ~ohnrobe2~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Thayan Circlet
    COPY_EXISTING ~dcowl1.cre~ ~override~
      ADD_CRE_ITEM ~ohnhelm1~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // The Brick +2
    // Tunic of Blindeye
    COPY_EXISTING ~trmer02.sto~ ~override~
      ADD_STORE_ITEM ~ohnhamm1~ AFTER ~hamm06~ #0 #0 #0 ~IDENTIFIED~ #1
      ADD_STORE_ITEM ~ohnrobe3~ LAST #1 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Shield of Fyrus Khal +2
    COPY_EXISTING ~ribald3.sto~ ~override~
      ADD_STORE_ITEM ~ohnshld1~ AFTER ~shld30~ #1 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Wild Sling +1
    COPY_EXISTING ~shop05.sto~ ~override~
      ADD_STORE_ITEM ~ohnslng1~ AFTER ~slng01~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Wand of Whips
    COPY_EXISTING ~hlmage.cre~ ~override~
      ADD_CRE_ITEM ~ohnwand1~ #50 #0 #0 ~NONE~ ~QITEM1 QITEM2 QITEM3 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Wooden Horse Necklace
    COPY_EXISTING ~tdr10a.cre~ ~override~
      ADD_CRE_ITEM ~ohnamul2~ #0 #0 #0 ~NONE~ ~AMULET INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // <CHARNAME>'s Tankard
    COPY_EXISTING ~ohntank.itm~ ~override~
      SAY UNIDENTIFIED_DESC @50008  // This handsome pewter tankard conforms to the shape of your hand...
    BUT_ONLY
    COMPILE ~%MOD_FOLDER%/dialogs/neera_items.d~

    // The Grave Binder +2
    COPY_EXISTING ~ar0805.are~ ~override~
      LPF ADD_AREA_ITEM
        INT_VAR
        container_to_add_to = 1
        STR_VAR
        item_to_add = ~DAGG04~
      END
    BUT_ONLY

    // Dagger of Venom +2
    COPY_EXISTING ~thief6.cre~ ~override~
      ADD_CRE_ITEM ~misc75~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Robe of the Neutral Archmagi
    COPY_EXISTING ~gorsta11.cre~ ~override~
      // make existing item available from one of the WK 1st floor statues
      LPF REMOVE_CRE_ITEM_FLAGS
        STR_VAR
          item_to_change = ~CLCK16~
          flags = ~UNDROPPABLE~
      END
    BUT_ONLY

    // Kazrah's Shield +4
    COPY_EXISTING ~werewyv2.cre~ ~override~
      ADD_CRE_ITEM ~ohbshld1~ #0 #0 #0 ~none~ ~inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
    BUT_ONLY

    // Cloak of the Lich
    COPY_EXISTING ~marlowe.cre~ ~override~
      ADD_CRE_ITEM ~ohbclck1~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
    COPY_EXISTING ~marlowe2.bcs~ ~override~
      // item is dropped when Marlowe is killed by the lich
      DECOMPILE_AND_PATCH BEGIN
        PATCH_IF (INDEX_BUFFER(~DropInventory()~) < 0) BEGIN
          REPLACE_TEXTUALLY ~\(ActionOverride("marlowe",ApplySpell(Myself,\)~ ~ActionOverride("marlowe",DropInventory())%LNL%\1~
        END
      END
    BUT_ONLY
  END
END


// Add Rasaad's items to the game
DEFINE_ACTION_FUNCTION A7ItemsRasaad
BEGIN
  // Moonlight Walkers usable by all Sun Soul Monks
  COPY_EXISTING ~rsboot.itm~ ~override~
    WRITE_LONG 0x1e 0x407FFFCA    // unusable by all but good-aligned monks of any race
    SET kit = IDS_OF_SYMBOL (~KIT~ ~SUN_SOUL~)
    PATCH_IF (kit < 0) BEGIN
      SET kit = 0x4025      // fall back to hardcoded value
    END
    LPF ALTER_ITEM_EFFECT
      INT_VAR
      check_globals = 1
      match_opcode = 319    // Restrict item
      parameter1 = kit      // IDS value: Sun Soul Monk
      parameter2 = 9        // IDS type: KIT.IDS
      special = RESOLVE_STR_REF (@50001)  // Sun Soul Monk
    END
  BUT_ONLY

  COPY_EXISTING ~item_use.2da~ ~override~
    LPF REMOVE_TEXT_LINE STR_VAR search = ~^[ %TAB%]*rsboot\b~ END
  BUT_ONLY

  // Glimmering Bands
  COPY_EXISTING ~rsbrac.itm~ ~override~
    WRITE_LONG 0x1e 0x407FFFC0    // unusable by all but monks of any race
    LPF DELETE_EFFECT
      INT_VAR
      check_globals = 1
      check_headers = 0
      match_opcode  = 319   // Restrict item
    END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING ~item_use.2da~ ~override~
    LPF REMOVE_TEXT_LINE STR_VAR search = ~^[ %TAB%]*rsbrac\b~ END
  BUT_ONLY

  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    // Moonlight Walkers
    COPY_EXISTING ~neira.cre~ ~override~
      ADD_CRE_ITEM ~rsboot~ #0 #0 #0 ~UNSTEALABLE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Glimmering Bands
    COPY_EXISTING ~sto4907.sto~ ~override~
      ADD_STORE_ITEM ~rsbrac~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Big-Fisted Belt
    COPY_EXISTING ~pridem.cre~ ~override~
      ADD_CRE_ITEM ~belt15~ #0 #0 #0 ~UNSTEALABLE~ ~BELT INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Moonlight Walkers
    COPY_EXISTING ~thief7.cre~ ~override~
      ADD_CRE_ITEM ~rsboot~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Glimmering Bands
    COPY_EXISTING ~shop03.sto~ ~override~
      ADD_STORE_ITEM ~rsbrac~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Ox-Tail Belt
    COPY_EXISTING ~cohrvale.cre~ ~override~
      ADD_CRE_ITEM ~ohrbelt1~ #0 #0 #0 ~NONE~ ~BELT INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Brooch of the Vagrant Blades
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~ohramul1~ AFTER ~amul14~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Belt of Minor Invulnerability
    COPY_EXISTING ~sahkng01.cre~ ~override~
      ADD_CRE_ITEM ~ohrbelt2~ #1 #0 #0 ~NONE~ ~BELT INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Ring of Duplication
    COPY_EXISTING ~bmthief.sto~ ~override~
      ADD_STORE_ITEM ~ohrring1~ AFTER ~shld08~ #3 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Hojar's Fame +1
    COPY_EXISTING ~bregg.cre~ ~override~
      ADD_CRE_ITEM ~ohrax1h1~ #0 #0 #0 ~NONE~ ~WEAPON1 WEAPON2 WEAPON3 WEAPON4 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Lupine Sling +2
    COPY_EXISTING ~uhmer01.sto~ ~override~
      ADD_STORE_ITEM ~ohrslng1~ AFTER ~slng01~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Cowl of the Stars
    COPY_EXISTING ~demfig02.cre~ ~override~
      ADD_CRE_ITEM ~ohrclck1~ #1 #0 #0 ~NONE~ ~CLOAK INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Hawksight +2
    COPY_EXISTING ~trmer04.sto~ ~override~
      ADD_STORE_ITEM ~ohrsw1h1~ AFTER ~sw1h22~ #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Cloak of Unerring Strikes
    COPY_EXISTING ~bernard2.sto~ ~override~
      ADD_STORE_ITEM ~ohrclck2~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Night Walkers
    // Sun Soulbeam
    COPY_EXISTING ~hgkar01.sto~ ~override~
      ADD_STORE_ITEM ~ohrboot1~ AFTER ~helm24~ #3 #0 #0 ~IDENTIFIED~ #1
      ADD_STORE_ITEM ~sw1hwk~ AFTER ~sw1h76~ #5 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Gauntlets of Aln Zekk
    COPY_EXISTING ~hlolaf.cre~ ~override~
      ADD_CRE_ITEM ~ohrbrac1~ #1 #0 #0 ~NONE~ ~GLOVES INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Cloak of Atonement
    COPY_EXISTING ~uddrow23.sto~ ~override~
      ADD_STORE_ITEM ~ohrclck3~ AFTER ~clck17~ #50 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Cloak of the Dark Moon
    COPY_EXISTING ~ar18prie.cre~ ~override~
      ADD_CRE_ITEM ~ohrclck4~ #3 #0 #0 ~NONE~ ~CLOAK INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Eyes of the Beholder
    COPY_EXISTING ~udmaster.cre~ ~override~
      ADD_CRE_ITEM ~ohreyeb~ #3 #3 #3 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY

    // Headband of the Devout
    COPY_EXISTING ~amsmug02.sto~ ~override~
      ADD_STORE_ITEM ~ohrhelm1~ AFTER ~misc3p~ #1 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY

    // Gem of Seeing
    COPY_EXISTING ~gaal01.cre~ ~override~
      ADD_CRE_ITEM ~ohrscrl1~ #1 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    BUT_ONLY
  END
END


// Specialized ALTER_SPELL_EFFECT: Updates primary parameters based on matching opcode and resource
DEFINE_PATCH_FUNCTION ALTER_SPELL_EFFECT_RESREF
INT_VAR
  header          = 0   // 0 = all headers
  match_opcode    = "-1"
  opcode          = "-1"
  parameter1      = "-1"
  parameter2      = "-1"
STR_VAR
  match_resource  = ~~
  resource        = ~~
BEGIN
  READ_ASCII 0 sig (8)
  PATCH_IF (~%sig%~ STR_EQ ~SPL V1  ~) BEGIN
    SET header -= 1
    READ_LONG 0x64 ofs_abils
    READ_SHORT 0x68 num_abils
    READ_LONG 0x6a ofs_effects
    FOR (abil = 0; abil < num_abils; ++abil) BEGIN
      SET ofs_abil = ofs_abils + abil * 0x28
      PATCH_IF (header < 0 || header = abil) BEGIN
        READ_SHORT (ofs_abil + 0x1e) num_effects
        READ_SHORT (ofs_abil + 0x20) idx_effects
        FOR (fx = 0; fx < num_effects; ++fx) BEGIN
          SET ofs_fx = ofs_effects + (idx_effects + fx) * 0x30
          READ_SHORT (ofs_fx + 0x00) op
          READ_ASCII (ofs_fx + 0x14) res (8) NULL
          PATCH_IF ((match_opcode = "-1" || op = match_opcode) && ~%res%~ STR_EQ ~%match_resource%~) BEGIN
            PATCH_IF (opcode != "-1") BEGIN WRITE_SHORT (ofs_fx + 0x00) opcode END
            PATCH_IF (parameter1 != "-1") BEGIN WRITE_LONG (ofs_fx + 0x04) parameter1 END
            PATCH_IF (parameter2 != "-1") BEGIN WRITE_LONG (ofs_fx + 0x08) parameter2 END
            WRITE_ASCIIE (ofs_fx + 0x14) ~%resource%~ (8)
          END
        END
      END
    END
  END ELSE BEGIN
    PATCH_WARN ~Not a valid SPL resource. Skipping operation.~
  END
END


DEFINE_ACTION_FUNCTION UPDATE_EPILOG_PORTRAIT
STR_VAR
  match_portrait = ~~ // original portrait BMP resref as regexp
  portrait = ~~
BEGIN
  COPY_EXISTING_REGEXP ~^.+\.2da$~ ~override~
    COUNT_2DA_ROWS 1 numRows
    PATCH_IF (numRows > 2) BEGIN
      READ_2DA_ENTRY 1 0 1 value
      PATCH_IF (~%value%~ STRING_MATCHES_REGEXP ~\*%match_portrait%~ = 0) BEGIN
        SET_2DA_ENTRY 1 0 1 ~*%portrait%~
      END
    END
  BUT_ONLY
END


// Removes a line of text from the current file
DEFINE_PATCH_FUNCTION REMOVE_TEXT_LINE
INT_VAR
  line = 0
STR_VAR
  search = ~~
RET
  line  // line number (-1 if no line is removed)
BEGIN
  SET line = "-1"
  SET ofs = 0
  FOR (i = 0; i >= 0 && ofs >= 0; ++i) BEGIN
    SET ofs2 = INDEX_BUFFER(~[%WNL%]~ ofs)
    SET ofs2 = (ofs2 < 0) ? INDEX_BUFFER(~$~ ofs) : ofs2
    PATCH_IF (ofs2 >= ofs) BEGIN
      READ_ASCII ofs text (ofs2 - ofs)
      PATCH_IF (i >= line) BEGIN
        PATCH_IF (~%text%~ STRING_CONTAINS_REGEXP ~%search%~ = 0) BEGIN
          SET ofs3 = INDEX_BUFFER(~[^%WNL%]~ ofs2)
          SET ofs2 = (ofs3 < 0) ? INDEX_BUFFER(~$~ ofs2) : ofs3
          PATCH_IF (ofs2 >= ofs) BEGIN
            DELETE_BYTES ofs (ofs2 - ofs)
            SET line = i
          END
          SET i = "-2"
        END ELSE BEGIN
          SET ofs = INDEX_BUFFER(~[^%WNL%]~ ofs2)
          SET ofs = (ofs <= ofs2) ? "-1" : ofs
        END
      END ELSE BEGIN
        SET ofs = INDEX_BUFFER(~[^%WNL%]~ ofs2)
        SET ofs = (ofs <= ofs2) ? "-1" : ofs
      END
    END ELSE BEGIN
      SET ofs = "-1"
    END
  END
END
