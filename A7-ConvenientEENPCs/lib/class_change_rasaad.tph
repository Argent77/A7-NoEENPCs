// Performs a full class/kit change for Rasaad

INCLUDE ~%MOD_FOLDER%/lib/class_change_defs.tph~

DEFINE_ACTION_FUNCTION a7#change_class_rasaad
INT_VAR
  reset = 0
  interactive = 0
  class = "-1"
  kit = "-1"
  kit1 = "-1"
  kit2 = "-1"
  kit3 = "-1"
BEGIN
  OUTER_SET cre_suffix = (GAME_IS ~bgee~) ? 2 : 8
  OUTER_SPRINT cre_reference ~rasaad%cre_suffix%~
  OUTER_SPRINT cre_resref_regexp ~^rasaad[0-9]*~

  ACTION_IF (NOT reset) BEGIN
    ACTION_DEFINE_ARRAY profs_warrior BEGIN
      ~PROFICIENCYKATANA~
      ~PROFICIENCYSCIMITARWAKISASHININJATO~
      ~PROFICIENCYSCIMITARWAKISASHININJATO~
      ~PROFICIENCYCROSSBOW~
      ~PROFICIENCY2WEAPON~
      ~PROFICIENCYFLAILMORNINGSTAR~
      ~PROFICIENCY2WEAPON~
      ~PROFICIENCYFLAILMORNINGSTAR~
      ~PROFICIENCYCROSSBOW~
    END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY thieving_weights BEGIN
      // parameter => weight
      ~pick_pockets~    => 0
      ~open_locks~      => 50
      ~find_traps~      => 60
      ~move_silently~   => 30
      ~hide_in_shadows~ => 40
      ~detect_illusion~ => 30
      ~set_traps~       => 0
    END
  END

  // allow only fighter and priest classes
  ACTION_DEFINE_ARRAY class_whitelist BEGIN
    ~FIGHTER~
    ~CLERIC~
    ~PALADIN~
    ~FIGHTER_MAGE~
    ~FIGHTER_CLERIC~
    ~RANGER~
    ~CLERIC_MAGE~
    ~FIGHTER_MAGE_CLERIC~
    ~CLERIC_RANGER~
    ~MONK~
  END
  // ACTION_DEFINE_ARRAY kit_whitelist BEGIN END
  // ACTION_DEFINE_ARRAY class_blacklist BEGIN END
  // ACTION_DEFINE_ARRAY kit_blacklist BEGIN END

  ACTION_IF (class == IDS_OF_SYMBOL(~class~ ~CLERIC~)) BEGIN
    // saving character level for later
    COPY_EXISTING_REGEXP ~%cre_resref_regexp%\.cre$~ ~override~
      READ_BYTE 0x234 level
      SET $cre(~%SOURCE_RES%~) = level
    BUT_ONLY IF_EXISTS
  END

  OUTER_SET chr_override = 17
  LAF a7#change_class_npc
    INT_VAR
      reset interactive class kit kit1 kit2 kit3
      chr_override
    STR_VAR
      npc_name = ~rasaad~
      cre_reference cre_resref_regexp
      class_whitelist_name = ~class_whitelist~
      // kit_whitelist_name = ~kit_whitelist~
      // class_blacklist_name = ~class_blacklist~
      // kit_blacklist_name = ~kit_blacklist~
    RET class_id kit_id
  END

  ACTION_IF (NOT reset) BEGIN
    OUTER_SET class = class_id
    OUTER_SET kit = kit_id
    LAF a7#is_class_included INT_VAR class class_aspect = IDS_OF_SYMBOL(~class~ ~MONK~) RET is_monk = result END
    LAF a7#is_class_included INT_VAR class class_aspect = IDS_OF_SYMBOL(~class~ ~PALADIN~) RET is_paladin = result END
    LAF a7#is_class_included INT_VAR class class_aspect = IDS_OF_SYMBOL(~class~ ~CLERIC~) RET is_cleric = result END

    // adjusting stats, equipment and biography
    ACTION_IF (is_cleric || is_paladin) BEGIN
      LAF a7#process_cre
        STR_VAR
          function_0 = ~a7#cre_stats_update_rasaad~
          function_1 = ~a7#cre_equipment_update_rasaad~
          function_2 = ~a7#cre_bio_update_rasaad~
      END
    END

    ACTION_IF (NOT is_monk) BEGIN
      // adjusting NPC-related items
      COPY_EXISTING ~rsboot.itm~ ~override~
        SAY IDENTIFIED_DESC @50002
      BUT_ONLY IF_EXISTS

      COPY_EXISTING ~rsbrac.itm~ ~override~
        SAY IDENTIFIED_DESC @50003
      BUT_ONLY IF_EXISTS
    END
  END
END


DEFINE_PATCH_FUNCTION a7#cre_stats_update_rasaad
BEGIN
  // adjusting stats to meet class requirements
  PATCH_IF (is_paladin) BEGIN
    SET chr_inc = chr_override - (BYTE_AT 0x23e)
    SET chr_inc = (chr_inc < 0) ? 0 : chr_inc
    PATCH_IF (chr_inc > 0) BEGIN
      WRITE_BYTE 0x23e (THIS + chr_inc) // CHR
    END

    SET int_dec = chr_inc >= 1 ? 1 : chr_inc
    WRITE_BYTE 0x23a (THIS - int_dec) // INT
    SET chr_inc -= int_dec

    SET dex_dec = chr_inc >= 2 ? 2 : chr_inc
    WRITE_BYTE 0x23c (THIS - dex_dec) // DEX
  END
END


DEFINE_PATCH_FUNCTION a7#cre_equipment_update_rasaad
BEGIN
  PATCH_IF (is_cleric) BEGIN
    SET level = VARIABLE_IS_SET $cre(~%SOURCE_RES%~) ? $cre(~%SOURCE_RES%~) : BYTE_AT 0x234
    PATCH_IF (GAME_IS ~bgee eet~ && level < 6) BEGIN
      ADD_CRE_ITEM ~blun04q~ #0 #0 #0 ~identified~ ~weapon1~ EQUIP
      ADD_CRE_ITEM ~slng01~ #0 #0 #0 ~identified~ ~weapon2~
      ADD_CRE_ITEM ~bull01~ #40 #0 #0 ~identified~ ~quiver1~
    END ELSE PATCH_IF (level < 13) BEGIN
      ADD_CRE_ITEM ~blun05~ #0 #0 #0 ~identified~ ~weapon1~ EQUIP
      ADD_CRE_ITEM ~slng02~ #0 #0 #0 ~identified~ ~weapon2~
      ADD_CRE_ITEM ~bull02~ #40 #0 #0 ~identified~ ~quiver1~
    END ELSE BEGIN
      ADD_CRE_ITEM ~blun15~ #0 #0 #0 ~identified~ ~weapon1~ EQUIP
      ADD_CRE_ITEM ~slng04~ #0 #0 #0 ~identified~ ~weapon2~
      ADD_CRE_ITEM ~bull03~ #40 #0 #0 ~identified~ ~quiver1~
      ADD_CRE_ITEM ~bull04~ #20 #0 #0 ~identified~ ~quiver2~
    END
  END
END


DEFINE_PATCH_FUNCTION a7#cre_bio_update_rasaad
BEGIN
  // Updating biography (BG2 only)
  PATCH_IF (NOT is_monk) BEGIN
    SET level = VARIABLE_IS_SET $cre(~%SOURCE_RES%~) ? $cre(~%SOURCE_RES%~) : BYTE_AT 0x234
    PATCH_IF (GAME_IS ~bg2ee~ || level > 7) BEGIN
      SET strref = RESOLVE_STR_REF(@51050)
      GET_STRREF strref text
      PATCH_IF (NOT ~%text%~ STR_EQ ~~) BEGIN // safety check if line hasn't been translated yet
        WRITE_LONG BIO strref
      END
    END
  END
END
