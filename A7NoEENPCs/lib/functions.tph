// Various functions for disabling individual NPCs

DEFINE_ACTION_FUNCTION A7DisableBaeloth
BEGIN
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    ACTION_IF (GAME_IS ~eet~) BEGIN
      OUTER_TEXT_SPRINT script ~BG2900.BCS~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT script ~AR2900.BCS~
    END
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%~) BEGIN
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BAINVI","GLOBAL",0)~ ~False()~
        END BUT_ONLY
    END
  END
END

DEFINE_ACTION_FUNCTION A7DisableDorn
BEGIN
  ACTION_IF (GAME_IS ~eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~BG2301.BCS~ ~BG4800.BCS~ ~OH1000.BCS~ ~AR0900.BCS~ ~FATESP.DLG~ END
  END ELSE ACTION_IF (GAME_IS ~bgee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR2301.BCS~ ~AR4800.BCS~ ~OH1000.BCS~ END
  END ELSE ACTION_IF (GAME_IS ~bg2ee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR0900.BCS~ ~FATESP.DLG~ END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN END
  END
  LAF A7Disable#DLC STR_VAR file_array = a7files END
  
  ACTION_IF (FILE_EXISTS_IN_GAME ~bd2000.bcs~) BEGIN
    COPY_EXISTING ~bd2000.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("bd_dorn_spawn","bd2000",0)~ ~False()~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BD_dorn_map_note","BD2000",0)~ ~False()~
      END BUT_ONLY
  END
END

DEFINE_ACTION_FUNCTION A7DisableHexxat
BEGIN
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR0406.BCS~ ~AR5501.BCS~ ~OH7000.BCS~ ~FATESP.DLG~ END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN END
  END

  LAF A7Disable#DLC STR_VAR file_array = a7files END
END

DEFINE_ACTION_FUNCTION A7DisableNeera
BEGIN
  ACTION_IF (GAME_IS ~eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~BG3300.BCS~ ~OHNMCUT1.BCS~ ~FATESP.DLG~ END
  END ELSE ACTION_IF (GAME_IS ~bgee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR3300.BCS~ END
  END ELSE ACTION_IF (GAME_IS ~bg2ee~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~OHNMCUT1.BCS~ ~FATESP.DLG~ END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN END
  END
  LAF A7Disable#DLC STR_VAR file_array = a7files END

  ACTION_IF (FILE_EXISTS_IN_GAME ~bd2100.bcs~) BEGIN
    COPY_EXISTING ~bd2100.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BD_NEERA","BD2100",0)~ ~False()~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("BD_neera_map_note","BD2100",0)~ ~False()~
      END BUT_ONLY
  END
END

DEFINE_ACTION_FUNCTION A7DisableRasaad
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    ACTION_IF (GAME_IS ~eet~) BEGIN
      OUTER_TEXT_SPRINT rasaad_script ~BG4800.BCS~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT rasaad_script ~AR4800.BCS~
    END
    ACTION_IF (FILE_EXISTS_IN_GAME ~%rasaad_script%~) BEGIN
      COPY_EXISTING ~%rasaad_script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("RASAAD_SPAWN_STOP","GLOBAL",0)~ ~False()~
        END BUT_ONLY
    END

    ACTION_IF (FILE_EXISTS_IN_GAME ~bd7000.bcs~) BEGIN
      COPY_EXISTING ~bd7000.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("bd_rasaad_spawn","bd7000",0)~ ~False()~
        END BUT_ONLY
    END
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_DEFINE_ARRAY a7files BEGIN ~AR2000.BCS~ ~FATESP.DLG~ END
    LAF A7Disable#DLC STR_VAR file_array = a7files END

    ACTION_IF (FILE_EXISTS_IN_GAME ~ohr_join.bcs~) BEGIN
      COPY_EXISTING ~ohr_join.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("OHR_PLOT","GLOBAL",0)~ ~False()~
        END
    END
  END
END


DEFINE_ACTION_FUNCTION A7PassiveBaeloth
BEGIN
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    COMPILE ~%MOD_FOLDER%/scripts/cutba01.baf~
  END
END

DEFINE_ACTION_FUNCTION A7PassiveHexxat
BEGIN
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~OHHFAK.BCS~) BEGIN
      COPY_EXISTING ~OHHFAK.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~Num.*TimesTalkedTo(0)~ ~False()~
        END BUT_ONLY
    END
  END
END

DEFINE_ACTION_FUNCTION A7PassiveNeera
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    ACTION_IF (GAME_IS ~eet~) BEGIN
      OUTER_TEXT_SPRINT script ~NEERA_.BCS~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT script ~NEERA.BCS~
    END
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%~) BEGIN
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~^[%TAB% ]*Global("NEERA_MEETING",.+)~ ~False()~
        END BUT_ONLY
    END
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~OHNCUT1A.BCS~) BEGIN
      COPY_EXISTING ~OHNCUT1A.BCS~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("NEERA.+)~ ~~
        END BUT_ONLY
      EXTEND_BOTTOM ~ohncut1a.bcs~ ~%MOD_FOLDER%/scripts/ohncut1a_neera.baf~
      COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts/ohnmcut1.baf~
      COMPILE ~%MOD_FOLDER%/dialogs/neera_bg2.d~
    END
  END
END

DEFINE_ACTION_FUNCTION A7PassiveRasaad
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    ACTION_IF (GAME_IS ~eet~) BEGIN
      OUTER_TEXT_SPRINT script ~RASAAD_.BCS~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT script ~RASAAD.BCS~
    END
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%~) BEGIN
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          // Rasaad should not interfere with the Nimbul or Neira fights
          SET ofs = 0
          WHILE (ofs >= 0) BEGIN
            SET ofs = INDEX_BUFFER(~"NIMBUL"~ (ofs + 1))
            PATCH_IF (ofs >= 0) BEGIN
              SET ofs1 = RINDEX_BUFFER(CASE_SENSITIVE ~^IF~ ofs)
              SET ofs2 = INDEX_BUFFER(CASE_SENSITIVE ~^END~ ofs)
              // Disable attack
              SET ofs3 = INDEX_BUFFER(~^[ %TAB%]*Attack.*(~ ofs1)
              PATCH_IF (ofs3 > ofs1 && ofs3 < ofs2) BEGIN
                TEXT_SPRINT comment ~//~
                SET comment_len = STRING_LENGTH ~%comment%~
                INSERT_BYTES ofs3 comment_len
                WRITE_ASCIIE ofs3 ~%comment%~ (comment_len)
                SET ofs2 += comment_len
              END
              // Prevent auto-talk
              SET ofs3 = INDEX_BUFFER(~^[ %TAB%]*Dialog.*(~ ofs1)
              PATCH_IF (ofs3 > ofs1 && ofs3 < ofs2) BEGIN
                TEXT_SPRINT talked ~SetNumTimesTalkedTo(1) //~
                SET talked_len = STRING_LENGTH ~%talked%~
                INSERT_BYTES ofs3 talked_len
                WRITE_ASCIIE ofs3 ~%talked%~ (talked_len)
                SET ofs2 += talked_len
              END
            END
          END
        END BUT_ONLY
    END
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~RASAAD.BCS~) BEGIN
      COPY_EXISTING ~ar2000.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("RASAAD8",.*)~ ~CreateCreature("RASAAD8",[1832.1410],SSE)~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~MoveGlobal("AR2000","Rasaad",.*)~ ~MoveGlobal("AR2000","Rasaad",[1832.1410])~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("Rasaad",Face(.*))~ ~ActionOverride("Rasaad",Face(SSE))~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("OHRTREYA",.*)~ ~~
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~CreateCreature("OHRSSM01",.*)~ ~~
        END BUT_ONLY
      COPY_EXISTING ~ohrssm01.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE ~Range(\[PC\],.*)~ ~Detect([PC])~
        END BUT_ONLY
      COMPILE ~%MOD_FOLDER%/dialogs/rasaad_bg2.d~
    END
  END
END


// Internally used
DEFINE_ACTION_FUNCTION A7Disable#DLC
  STR_VAR
    file_array  = ~~
BEGIN
  ACTION_IF VARIABLE_IS_SET $EVAL "%file_array%"(0) BEGIN
    ACTION_PHP_EACH "%file_array%" AS _ => filename BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%filename%~) BEGIN
        COPY_EXISTING ~%filename%~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE ~HasDLC(.+)~ ~False()~
          END BUT_ONLY
      END
    END
  END
END


// Add Baeloth's items to the game
DEFINE_ACTION_FUNCTION A7ItemsBaeloth
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~ AND NOT FILE_EXISTS_IN_GAME ~bd1000.bcs~) BEGIN   // excluding SoD
    // Slave Ring
    COPY_EXISTING ~baring.itm~ ~override~
      WRITE_LONG NAME1 6348   // Ring
      WRITE_LONG 0x18 ((LONG_AT 0x18) | 0x04 | 0x10 | 0x40)   // enabling Droppable, Cursed and Magical flags
      WRITE_BYTE 0x2f ((BYTE_AT 0x2f) | 0x02)   // Unusable by: Wizard Slayer
      WRITE_LONG 0x34 50    // Price
      WRITE_SHORT 0x42 50   // Lore
      WRITE_LONG UNIDENTIFIED_DESC 17054  // Generic ring description...
      SAY IDENTIFIED_DESC @50000          // This ring has been known to be in possession of a drow torturer...
      LPF ALTER_ITEM_EFFECT
        INT_VAR
        check_headers = 1
        match_opcode = 122    // Create inventory item
        parameter1 = 1        // # to create
      END
      LPF DELETE_ITEM_EQEFFECT
        INT_VAR
        opcode_to_delete = 319  // Restrict item to Baeloth
      END

    COPY_EXISTING ~stocheap.sto~ ~override~
      ADD_STORE_ITEM ~baring~ FIRST #1 #0 #0 ~NONE~ #1
  END
END


// Add Dorn's items to the game
DEFINE_ACTION_FUNCTION A7ItemsDorn
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    COPY_EXISTING ~ulgoth.sto~ ~override~
      // Gloves of Missile Snaring
      ADD_STORE_ITEM ~brac18~ LAST #0 #0 #0 ~IDENTIFIED~ #1

    COPY_EXISTING ~taerum.sto~ ~override~
      // Golden Girdle of Urnst
      ADD_STORE_ITEM ~belt02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
      // Albruin +1
      ADD_STORE_ITEM ~sw1h34~ LAST #1 #0 #0 ~IDENTIFIED~ #1
    // Elven Chain Mail
      ADD_STORE_ITEM ~chan12~ AFTER ~chan01~ #0 #0 #0 ~IDENTIFIED~ #1

    COPY_EXISTING ~inn3351.sto~ ~override~
      // Amulet of Spell Warding
      ADD_STORE_ITEM ~amul25~ AT 1 #0 #0 #0 ~IDENTIFIED~ #1
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Silver Dragon Scales
    COPY_EXISTING ~udsilver.cre~ ~override~
      ADD_CRE_ITEM ~ohdscale~ #0 #0 #0 ~UNSTEALABLE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    COPY_EXISTING ~gorwom02.cre~ ~override~
      ADD_CRE_ITEM ~ohdscale~ #0 #0 #0 ~UNSTEALABLE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Holy Long Sword of Tyr +3
    COPY_EXISTING ~ar3014.are~ ~override~
      LPF ADD_AREA_ITEM
        INT_VAR
        container_to_add_to = 1
        STR_VAR
        item_to_add = ~OHDHSWD~
      END

    // Helm of the Noble +1
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~helm03~ AFTER ~helm13~ #0 #0 #0 ~IDENTIFIED~ #1
    COPY_EXISTING ~trmer04.sto~ ~override~
      ADD_STORE_ITEM ~helm03~ FIRST #0 #0 #0 ~IDENTIFIED~ #1

    // Rod of Shadowstep
    COPY_EXISTING ~amsaemon.cre~ ~override~
      ADD_CRE_ITEM ~ohdrod~ #5 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Potion of Clairvoyance
    COPY_EXISTING ~roger.sto~ ~override~
      ADD_STORE_ITEM ~ohdpotn1~ LAST #1 #0 #0 ~IDENTIFIED~ #1
    COPY_EXISTING ~sartem01.sto~ ~override~
      ADD_STORE_ITEM ~ohdpotn1~ AFTER ~potn20~ #1 #0 #0 ~IDENTIFIED~ #1

    // Abyssal Blade
    COPY_EXISTING ~suraam.cre~ ~override~
      ADD_CRE_ITEM ~ohdsw2ha~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // The Brass Blade +5
    COPY_EXISTING ~hgber01.cre~ ~override~
      ADD_CRE_ITEM ~ohdsw03~ #1 #0 #0 ~NONE~ ~WEAPON1 WEAPON2 WEAPON3 WEAPON4 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
  END
END


// Add Hexxat's items to the game
DEFINE_ACTION_FUNCTION A7ItemsHexxat
BEGIN
  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Ring of Anti-Venom
    COPY_EXISTING ~maevar.cre~ ~override~
      ADD_CRE_ITEM ~ring46~ #0 #0 #0 ~NONE~ ~LRING RRING INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Worn Whispers
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~boot02~ LAST #0 #0 #0 ~IDENTIFIED~ #1

    // The Jade Fang
    COPY_EXISTING ~ribald3.sto~ ~override~
      ADD_STORE_ITEM ~ohhdag01~ AFTER ~dagg15~ #0 #0 #0 ~IDENTIFIED~ #1
  END
END


// Add Neera's items to the game
DEFINE_ACTION_FUNCTION A7ItemsNeera
BEGIN
  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    COPY_EXISTING ~friend.sto~ ~override~
      // Gem Bag
      ADD_STORE_ITEM ~bag02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    COPY_EXISTING ~inn3351.sto~ ~override~
      // Adoy's Belt
      ADD_STORE_ITEM ~nebelt01~ AT 1 #0 #0 #0 ~IDENTIFIED~ #1
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Adoy's Belt
    // Robe of Invocation
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~nebelt01~ AFTER ~amul14~ #0 #0 #0 ~IDENTIFIED~ #1
      ADD_STORE_ITEM ~ohnrobe2~ LAST #0 #0 #0 ~IDENTIFIED~ #1

    // Thayan Circlet
    COPY_EXISTING ~dcowl1.cre~ ~override~
      ADD_CRE_ITEM ~ohnhelm1~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // The Brick +2
    // Tunic of Blindeye
    COPY_EXISTING ~trmer02.sto~ ~override~
      ADD_STORE_ITEM ~ohnhamm1~ AFTER ~hamm06~ #0 #0 #0 ~IDENTIFIED~ #1
      ADD_STORE_ITEM ~ohnrobe3~ LAST #1 #0 #0 ~IDENTIFIED~ #1

    // Shield of Fyrus Khal +2
    COPY_EXISTING ~ribald3.sto~ ~override~
      ADD_STORE_ITEM ~ohnshld1~ AFTER ~shld30~ #1 #0 #0 ~IDENTIFIED~ #1

    // Wild Sling +1
    COPY_EXISTING ~shop05.sto~ ~override~
      ADD_STORE_ITEM ~ohnslng1~ AFTER ~slng01~ #0 #0 #0 ~IDENTIFIED~ #1

    // Wand of Whips
    COPY_EXISTING ~hlmage.cre~ ~override~
      ADD_CRE_ITEM ~ohnwand1~ #50 #0 #0 ~NONE~ ~QITEM1 QITEM2 QITEM3 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
  END
END


// Add Rasaad's items to the game
DEFINE_ACTION_FUNCTION A7ItemsRasaad
BEGIN
  // Moonlight Walkers usable by all Sun Soul Monks
  COPY_EXISTING ~rsboot.itm~ ~override~
    WRITE_LONG 0x1e 0x407FFFCA    // unusable by all but good-aligned monks of any race
    SET kit = IDS_OF_SYMBOL (~KIT~ ~SUN_SOUL~)
    PATCH_IF (kit < 0) BEGIN
      SET kit = 0x4025      // fall back to hardcoded value
    END
    LPF ALTER_ITEM_EFFECT
      INT_VAR
      check_globals = 1
      match_opcode = 319    // Restrict item
      parameter1 = kit      // IDS value: Sun Soul Monk
      parameter2 = 9        // IDS type: KIT.IDS
      special = RESOLVE_STR_REF (@50001)  // Sun Soul Monk
    END

  // BG1 portion
  ACTION_IF (GAME_IS ~bgee eet~) BEGIN
    // Moonlight Walkers
    COPY_EXISTING ~neira.cre~ ~override~
      ADD_CRE_ITEM ~rsboot~ #0 #0 #0 ~UNSTEALABLE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Glimmering Bands
    COPY_EXISTING ~rsbrac.itm~ ~override~
      WRITE_LONG 0x1e 0x407FFFC0    // unusable by all but monks of any race
      LPF DELETE_EFFECT
        INT_VAR
        check_globals = 1
        check_headers = 0
        match_opcode  = 319   // Restrict item
      END

    COPY_EXISTING ~sto4907.sto~ ~override~
      ADD_STORE_ITEM ~rsbrac~ LAST #0 #0 #0 ~IDENTIFIED~ #1

    COPY_EXISTING ~pridem.cre~ ~override~
      // Big-Fisted Belt
      ADD_CRE_ITEM ~belt15~ #0 #0 #0 ~UNSTEALABLE~ ~BELT INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
  END

  // BG2 portion
  ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
    // Moonlight Walkers
    COPY_EXISTING ~thief7.cre~ ~override~
      ADD_CRE_ITEM ~rsboot~ #0 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
    
    // Ox-Tail Belt
    COPY_EXISTING ~cohrvale.cre~ ~override~
      ADD_CRE_ITEM ~ohrbelt1~ #0 #0 #0 ~NONE~ ~BELT INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Brooch of the Vagrant Blades
    COPY_EXISTING ~ribald.sto~ ~override~
      ADD_STORE_ITEM ~ohramul1~ AFTER ~amul14~ #0 #0 #0 ~IDENTIFIED~ #1

    // Belt of Minor Invulnerability
    COPY_EXISTING ~sahkng01.cre~ ~override~
      ADD_CRE_ITEM ~ohrbelt2~ #1 #0 #0 ~NONE~ ~BELT INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Ring of Duplication
    COPY_EXISTING ~bmthief.sto~ ~override~
      ADD_STORE_ITEM ~ohrring1~ AFTER ~shld08~ #3 #0 #0 ~IDENTIFIED~ #1

    // Hojar's Fame +1
    COPY_EXISTING ~bregg.cre~ ~override~
      ADD_CRE_ITEM ~ohrax1h1~ #0 #0 #0 ~NONE~ ~WEAPON1 WEAPON2 WEAPON3 WEAPON4 INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Lupine Sling +2
    COPY_EXISTING ~uhmer01.sto~ ~override~
      ADD_STORE_ITEM ~ohrslng1~ AFTER ~slng01~ #0 #0 #0 ~IDENTIFIED~ #1

    // Cowl of the Stars
    COPY_EXISTING ~demfig02.cre~ ~override~
      ADD_CRE_ITEM ~ohrclck1~ #1 #0 #0 ~NONE~ ~CLOAK INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Hawksight +2
    COPY_EXISTING ~trmer04.sto~ ~override~
      ADD_STORE_ITEM ~ohrsw1h1~ AFTER ~sw1h22~ #0 #0 #0 ~IDENTIFIED~ #1

    // Cloak of Unerring Strikes
    COPY_EXISTING ~bernard2.sto~ ~override~
      ADD_STORE_ITEM ~ohrclck2~ LAST #0 #0 #0 ~IDENTIFIED~ #1

    // Night Walkers
    // Sun Soulbeam
    COPY_EXISTING ~hgkar01.sto~ ~override~
      ADD_STORE_ITEM ~ohrboot1~ AFTER ~helm24~ #3 #0 #0 ~IDENTIFIED~ #1
      ADD_STORE_ITEM ~sw1hwk~ AFTER ~sw1h76~ #5 #0 #0 ~IDENTIFIED~ #1

    // Gauntlets of Aln Zekk
    COPY_EXISTING ~hlolaf.cre~ ~override~
      ADD_CRE_ITEM ~ohrbrac1~ #1 #0 #0 ~NONE~ ~GLOVES INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Cloak of Atonement
    COPY_EXISTING ~uddrow23.sto~ ~override~
      ADD_STORE_ITEM ~ohrclck3~ AFTER ~clck17~ #50 #0 #0 ~IDENTIFIED~ #1

    // Cloak of the Dark Moon
    COPY_EXISTING ~ar18prie.cre~ ~override~
      ADD_CRE_ITEM ~ohrclck4~ #3 #0 #0 ~NONE~ ~CLOAK INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Eyes of the Beholder
    COPY_EXISTING ~udmaster.cre~ ~override~
      ADD_CRE_ITEM ~ohreyeb~ #3 #3 #3 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~

    // Headband of the Devout
    COPY_EXISTING ~amsmug02.sto~ ~override~
      ADD_STORE_ITEM ~ohrhelm1~ AFTER ~misc3p~ #1 #0 #0 ~IDENTIFIED~ #1

    // Gem of Seeing
    COPY_EXISTING ~gaal01.cre~ ~override~
      ADD_CRE_ITEM ~ohrscrl1~ #1 #0 #0 ~NONE~ ~INV1 INV2 INV3 INV4 INV5 INV6 INV7 INV8 INV9 INV10 INV11 INV12 INV13 INV14 INV15 INV16~
  END
END


// Specialized ALTER_SPELL_EFFECT: Updates primary parameters based on matching opcode and resource
DEFINE_PATCH_FUNCTION ALTER_SPELL_EFFECT_RESREF
INT_VAR
  header          = 0   // 0 = all headers
  match_opcode    = "-1"
  opcode          = "-1"
  parameter1      = "-1"
  parameter2      = "-1"
STR_VAR
  match_resource  = ~~
  resource        = ~~
BEGIN
  READ_ASCII 0 sig (8)
  PATCH_IF (~%sig%~ STR_EQ ~SPL V1  ~) BEGIN
    SET header -= 1
    READ_LONG 0x64 ofs_abils
    READ_SHORT 0x68 num_abils
    READ_LONG 0x6a ofs_effects
    FOR (abil = 0; abil < num_abils; ++abil) BEGIN
      SET ofs_abil = ofs_abils + abil * 0x28
      PATCH_IF (header < 0 || header = abil) BEGIN
        READ_SHORT (ofs_abil + 0x1e) num_effects
        READ_SHORT (ofs_abil + 0x20) idx_effects
        FOR (fx = 0; fx < num_effects; ++fx) BEGIN
          SET ofs_fx = ofs_effects + (idx_effects + fx) * 0x30
          READ_SHORT (ofs_fx + 0x00) op
          READ_ASCII (ofs_fx + 0x14) res (8) NULL
          PATCH_IF ((match_opcode = "-1" || op = match_opcode) && ~%res%~ STR_EQ ~%match_resource%~) BEGIN
            PATCH_IF (opcode != "-1") BEGIN WRITE_SHORT (ofs_fx + 0x00) opcode END
            PATCH_IF (parameter1 != "-1") BEGIN WRITE_LONG (ofs_fx + 0x04) parameter1 END
            PATCH_IF (parameter2 != "-1") BEGIN WRITE_LONG (ofs_fx + 0x08) parameter2 END
            WRITE_ASCIIE (ofs_fx + 0x14) ~%resource%~ (8)
          END
        END
      END
    END
  END ELSE BEGIN
    PATCH_WARN ~Not a valid SPL resource. Skipping operation.~
  END
END


DEFINE_ACTION_FUNCTION UPDATE_EPILOG_PORTRAIT
STR_VAR
  match_portrait = ~~ // original portrait BMP resref as regexp
  portrait = ~~
BEGIN
  COPY_EXISTING_REGEXP ~^.+\.2da$~ ~override~
    COUNT_2DA_ROWS 1 numRows
    PATCH_IF (numRows > 2) BEGIN
      READ_2DA_ENTRY 1 0 1 value
      PATCH_IF (~%value%~ STRING_MATCHES_REGEXP ~\*%match_portrait%~ = 0) BEGIN
        SET_2DA_ENTRY 1 0 1 ~*%portrait%~
      END
    END
  BUT_ONLY
END
